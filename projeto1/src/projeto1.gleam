import ambiente.{type Ambiente}
import fase1
import fase2
import fase3
import fase4
import gleam/int
import gleam/io
import gleam/list
import gleam/string
import jogador.{type Jogador}
import jogo

/// Ponte com o javascript para ler a entrada do usu√°rio-
@external(javascript, "../../../../readline_bridge.mjs", "question")
pub fn question(prompt: String) -> String

/// Fun√ß√£o para printar centralizado
pub fn print_centralizado(texto: String) {
  let largura_terminal = 72
  let tamanho_texto = string.length(texto)

  let padding = { largura_terminal - tamanho_texto } / 2

  let espacos = case padding > 0 {
    True -> string.repeat(" ", padding)
    False -> ""
  }

  io.println(espacos <> texto)
}

/// Inicia o jogo e executanto um loop
/// O loop verifica o n√≠vel de alerta do jogador e se ele venceu ou perdeu
fn game_loop(sala_atual: Ambiente, hacker: Jogador) {
  //Verifica se o n√≠vel de alerta estourou
  case hacker.nivel_alerta >= 100 {
    True -> {
      mostra_derrota()
    }
    False -> {
      //Caso n√£o estourou, verifica Vit√≥ria
      verifica_vitoria(hacker, sala_atual)
    }
  }
}

/// Exibe mensagem de derrota quando o n√≠vel de alerta chega a 100%
fn mostra_derrota() {
  io.println("\nüíÄ GAME OVER! üíÄ")
  io.println("      ,-=-.      ")
  io.println("     | ~~~ |     ")
  io.println("     |R.I.P|     ")
  io.println("     |_____|     ")
  io.println("    V,VV,,V,V    ")
  io.println("N√≠vel de Detec√ß√£o: 100%.")
  io.println(
    "Sua conex√£o foi rastreada. O hacker invandiu o seu computador tamb√©m",
  )
}

/// Verifica se o jogador tem o item "VITORIA_FINAL", que √© obtido ao completar a Fase 4
/// Se sim, exibe mensagem de vit√≥ria e encerra o jogo
/// Se n√£o, inicia a transi√ß√£o para a pr√≥xima fase (fase 1 -> fase 2)
fn verifica_vitoria(hacker: Jogador, sala_atual: Ambiente) {
  case jogador.tem_item(hacker, "VITORIA_FINAL") {
    True -> {
      io.println(
        "Parab√©ns! Jogo conclu√≠do com sucesso!\n Voc√™ impediu o hacker, agindo como um defensor do sistema!!",
      )
      Nil
    }
    False -> {
      transicao_fase2(hacker, sala_atual)
    }
  }
}

/// Transi√ß√µes entre a fase 2 -> fase 3, verificando os itens necess√°rios e o ambiente
/// Cada transi√ß√£o chama a fun√ß√£o `game_loop` com o novo ambiente e o jogador atualizado
fn transicao_fase2(hacker: Jogador, sala_atual: Ambiente) {
  case
    jogador.tem_item(hacker, "Conexao_SSH_Ativa")
    && string.contains(sala_atual.nome, "Disciplina")
  {
    True -> {
      fase2.contexto_f2()
      game_loop(fase2.ambiente_adm(), hacker)
    }
    False -> {
      // Transi√ß√£o 2 -> 3 (Requer: Credencial_Root_SQL)

      transicao_fase3(hacker, sala_atual)
    }
  }
}

///Transi√ß√µes entre a fase 3 -> fase 4, verificando os itens necess√°rios e o ambiente
/// Cada transi√ß√£o chama a fun√ß√£o `game_loop` com o novo ambiente e o jogador atualizado
fn transicao_fase3(hacker: Jogador, sala_atual: Ambiente) {
  case
    jogador.tem_item(hacker, "Credencial_Root_SQL")
    && string.contains(sala_atual.nome, "Terminal")
  {
    True -> {
      fase3.contexto_f3()
      game_loop(fase3.ambiente_cpu(), hacker)
    }
    False -> {
      // Transi√ß√£o 3 -> 4 (Requer: Acesso_Banco_Dados)
      transicao_fase4(hacker, sala_atual)
    }
  }
}

///Transi√ß√µes entre a fase 4 -> vitp, verificando os itens necess√°rios e o ambiente
/// Cada transi√ß√£o chama a fun√ß√£o `game_loop` com o novo ambiente e o jogador atualizado
fn transicao_fase4(hacker: Jogador, sala_atual: Ambiente) {
  case
    jogador.tem_item(hacker, "Acesso_Banco_Dados")
    && string.contains(sala_atual.nome, "Processador")
  {
    True -> {
      fase4.contexto_f4()
      game_loop(fase4.ambiente_servidor_sql(), hacker)
    }
    False -> {
      executar_rodada(sala_atual, hacker)
    }
  }
}

/// Fun√ß√£o auxiliar para processar comandos
/// Exibe o ambiente atual, os itens do jogador e os comandos dispon√≠veis
/// Dependendo do ambiente, exibe comandos de navega√ß√£o espec√≠ficos
/// Processa o comando do usu√°rio e chama a fun√ß√£o apropriada
/// Se o comando for "Sair", encerra o jogo
fn executar_rodada(sala_atual: Ambiente, hacker: Jogador) {
  io.println(
    "\n========================================================================\n",
  )
  print_centralizado("üìç " <> sala_atual.nome)
  print_centralizado(
    "ALERTA: "
    <> int.to_string(hacker.nivel_alerta)
    <> "% | ITENS: "
    <> int.to_string(list.length(hacker.inventario)),
  )
  io.println(
    "________________________________________________________________________\n",
  )
  print_centralizado(
    "Comandos: [Investigar] [Pegar] [Hackear] [Mochila] [Dica]",
  )

  // Comandos de navega√ß√£o da Fase 1
  case string.contains(sala_atual.nome, "Disciplinas Dispon√≠veis") {
    True -> print_centralizado("Navega√ß√£o: [Ir 1] [Ir 2] [Ir 3] [Ir 4]")
    False -> Nil
  }
  case string.contains(sala_atual.nome, "Disciplina ") {
    True -> print_centralizado("Navega√ß√£o: [Voltar]")
    False -> Nil
  }

  let comando = question("\nDigite: ")

  case comando {
    "Sair" -> io.println("Desconectando...")

    "Investigar" -> {
      io.println(jogo.explorar(sala_atual, hacker))
      game_loop(sala_atual, hacker)
    }

    "Mochila" -> {
      io.println(
        "\n========================================================================",
      )
      print_centralizado("\nSEU INVENT√ÅRIO:\n")
      list.each(hacker.inventario, fn(i) { io.println(" - " <> i) })
      game_loop(sala_atual, hacker)
    }

    "Pegar" -> {
      case list.first(sala_atual.objetos) {
        Ok(obj) -> {
          io.println(
            "\n========================================================================",
          )
          io.println(">> Interagindo com: " <> obj.nome)
          io.println("")
          io.println(obj.descricao)
          let novo_hacker = jogo.interagir(obj, hacker)
          game_loop(sala_atual, novo_hacker)
        }
        Error(Nil) -> {
          io.println(
            "\n========================================================================",
          )
          io.println("\n>> N√£o h√° nada √∫til aqui.")
          game_loop(sala_atual, hacker)
        }
      }
    }
    "Dica" -> {
      case list.first(sala_atual.enigmas) {
        Ok(enigma) -> {
          io.println(
            "\n========================================================================",
          )
          io.println("\nDICA:\n")
          list.each(enigma.dicas, fn(i) { io.println(" - " <> i) })
          game_loop(sala_atual, hacker)
        }
        Error(Nil) -> {
          io.println(
            "\n========================================================================",
          )
          io.println("\n>> N√£o h√° nada √∫til aqui.")
          game_loop(sala_atual, hacker)
        }
      }
    }
    "Hackear" -> {
      case list.first(sala_atual.enigmas) {
        Ok(enigma) -> {
          io.println(
            "\n========================================================================",
          )
          io.println(enigma.descricao)

          io.println("\nDigite 'Voltar' para retornar a p√°gina anterior")
          let tentativa = question("Digite a resposta: ")
          case tentativa == "Voltar" {
            True -> game_loop(sala_atual, hacker)
            False -> {
              let novo_hacker = jogo.resolver_enigma(enigma, tentativa, hacker)
              verifica_acerto(hacker, novo_hacker, sala_atual)
            }
          }
        }
        Error(Nil) -> {
          io.println(">> Nenhum sistema vulner√°vel detectado.")
          game_loop(sala_atual, hacker)
        }
      }
    }

    // Navega√ß√£o Fase 1
    "Ir 1" -> game_loop(fase1.entrar_no_ambiente(1), hacker)
    "Ir 2" -> game_loop(fase1.entrar_no_ambiente(2), hacker)
    "Ir 3" -> game_loop(fase1.entrar_no_ambiente(3), hacker)
    "Ir 4" -> game_loop(fase1.entrar_no_ambiente(4), hacker)
    "Voltar" -> game_loop(fase1.entrar_no_ambiente(0), hacker)

    _ -> {
      io.println("Comando desconhecido.")
      game_loop(sala_atual, hacker)
    }
  }
}

/// A fun√ß√£o verifica se o jogador acertou a resposta do enigma. Se sim, concede o 
/// acesso √† pr√≥xima fase, se n√£o, aumenta o n√≠vel de alerta em 20%.
fn verifica_acerto(hacker: Jogador, novo_hacker: Jogador, sala_atual: Ambiente) {
  case list.length(novo_hacker.inventario) > list.length(hacker.inventario) {
    True -> {
      io.println("\n‚úÖ ACESSO CONCEDIDO!")
      game_loop(sala_atual, novo_hacker)
    }
    False -> {
      io.println("\n‚ùå ACESSO NEGADO! O sistema detectou a intrus√£o.")
      io.println("‚ö†Ô∏è  N√≠vel de Alerta +20%")

      let hacker_punido = jogador.aumentar_alerta(hacker, 20)
      game_loop(sala_atual, hacker_punido)
      io.println("_________________________________________________________\n")
    }
  }
}

pub fn main() {
  let hacker_inicial = jogador.novo_jogador("Aluno", "Inicio")

  fase1.contexto_f1()
  let sala_inicial = fase1.ambiente_disciplina()

  game_loop(sala_inicial, hacker_inicial)
}
